reaction RotaryTableTracker
(:
	input signal previousSignal,
	input signal rotate,
	input signal externalSource,
	input signal receivedFromPrevious,
	input signal externalSink,
	output signal currentSignal,
	output signal sentToNext
) {
	signal beginSustain;
	signal endSustain;
	
	{
		while (true) {
			present(externalSink){
				emit endSustain;
			}
			
			pause;
		}
	}
	||
	{
		while (true) {
			present(externalSource){
				emit beginSustain;
			}
			
			pause;
		}
	}
	||
	{
		while (true) {
			
			await (rotate);
			
			present(currentSignal){
				emit sentToNext;
			}
			
			present(previousSignal && receivedFromPrevious){
				emit beginSustain;
			} else {
				emit endSustain;
			}
			
			await (!rotate);
		}
	}
	||
	{
		while (true) {
			// only one of these signals can happen per loop
			await (beginSustain);
			
			abort(endSustain){
				sustain currentSignal;
			}
			
		}
	}
}

PlantRotaryCD
(
	input signal rotaryTableTrigger;
	input signal plantConveyorActive;
	input signal plantConveyorBottleBeforePos1;
	output signal bottleAtPos1, bottleAtPos2, bottleAtPos3, bottleAtPos4, bottleAtPos5, bottleAtPos6;
)->{
	signal rotationInProgress, finishedRotating;
	signal deadSignal; // leave null
	signal conveyorToPos1;
	signal pos5ToConveyor;
	signal pos12, pos23, pos34, pos45, pos56, pos61; // connections
	signal pos1, pos2, pos3, pos4, pos5, pos6;
	
	// Simulate rotate the table
	{
		await(rotaryTableTrigger);
		
		trap(R){
			{
				waitl(1 s);
				exit(R);
			}
			||
			{
				sustain rotationInProgress;
			}
		}
		emit finishedRotating;
	}
	||
	// Conveyor to index 1
	{
		while (true) {
			present(!pos1 && plantConveyorActive && plantConveyorBottleBeforePos1){
				emit conveyorToPos1;
			}
			
			pause;
		}
	}
	||
	// Pos 5 to conveyor
	{
		while (true) {
			// bottle comes out even if there's something there
			present(pos5 && plantConveyorActive){
				emit pos5ToConveyor;
			}
			
			pause;
		}
	}
	||
	// Index 1
	{
		RotaryTableTracker(:
			pos6, 
			finishedRotating, 
			conveyorToPos1, 
			pos61, 
			deadSignal, 
			pos1, 
			pos12);
	}
	||
	// Index 2
	{
		RotaryTableTracker(:
			pos1, 
			finishedRotating, 
			deadSignal, 
			pos12, 
			deadSignal, 
			pos2, 
			pos23);
	}
	||
	// Index 3
	{
		RotaryTableTracker(:
			pos2, 
			finishedRotating, 
			deadSignal, 
			pos23, 
			deadSignal, 
			pos3, 
			pos34);
	}
	||
	// Index 4
	{
		RotaryTableTracker(:
			pos3, 
			finishedRotating, 
			deadSignal, 
			pos34, 
			deadSignal, 
			pos4, 
			pos45);
	}
	||
	// Index 5
	{
		RotaryTableTracker(:
			pos4, 
			finishedRotating, 
			deadSignal, 
			pos45, 
			pos5ToConveyor, 
			pos5, 
			pos56);
	}
	||
	// Index 6
	{
		RotaryTableTracker(:
			pos5, 
			finishedRotating, 
			deadSignal, 
			pos56, 
			deadSignal, 
			pos6, 
			pos61);
	}
	||
	// Trackers to output mapping
	{while(true){present(pos1){emit bottleAtPos1;} pause;}}
	||
	{while(true){present(pos2){emit bottleAtPos2;} pause;}}
	||
	{while(true){present(pos3){emit bottleAtPos3;} pause;}}
	||
	{while(true){present(pos4){emit bottleAtPos4;} pause;}}
	||
	{while(true){present(pos5){emit bottleAtPos5;} pause;}}
	||
	{while(true){present(pos6){emit bottleAtPos6;} pause;}}
}